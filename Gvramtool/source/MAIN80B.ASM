; LIMIT $A7FF (LZEを使う)
;WICSBUFA7E0h〜A7FFh A7E0h: ファイル名, A7FFh: リザルト
;LZEBUF A800h〜C7FFh (8192Bytes)
; LIMIT $C7FF (LZEを使わない)
;GVRAM  6000h〜7F3Fh (8000Bytes)
;RPG    C800h〜
;STACK  CE00h〜CEFFh
;BUF    CF00h〜EE3Fh (8000Bytes)

	ORG 0C800h
	; BASIC CALL ADDRESS
	JP	GVRAMTORAM			; USR($C800) GVRAM(8KB) → RAM BUF
	JP	RAMTOGVRAM			; USR($C803) BUF320x200(8KB) → GVRAM(320x200)
	RET					; USR($C806) なにもしない
	NOP
	NOP
	LD	HL, 0				; POKE$C80A,L:POKE$C80B,H:USR($C809) LZE展開
	JP	DECORD
	JP	SD_USR_LOAD			; USR($C80F,F$) MZ2000_SDバイナリロード
	JP	SD_USR_OPEN_CONCAT		; USR($C812,F$) MZ2000_SD連結ファイルオープン
	JP	SD_READ_CONCAT_1BLOCK		; USR($C815)    MZ2000_SD連結ファイルロード
	JP	SD_SKIP_CONCAT_1BLOCK		; USR($C818)    MZ2000_SD連結ファイルスキップ
	JP	SD_USR_FIND_CONCAT_1BLOCK	; USR($C81B,F$) MZ2000_SD連結ファイル検索
	JP	SD_TOP_CONCAT			; USR($C81E)    MZ2000_SD連結ファイル先頭へシーク
	JP	SD_CLOSE_CONCAT			; USR($C821)    MZ2000_SD連結ファイルクローズ
	JP	DRAW_MMZ			; USR($C824)    SD_USR_OPEN_CONCATでOPEN済みの連結ファイル内のMMZ画像1画面分表示
	JP	SD_USR_NEXT_FILE_EXIST		; USR($C827,R$) MZ2000_SD連結ファイルの次のデータがあるか
	JP	DRAW_MMZ_FILE			; USR($C82A,F$) 指定されたMMZファイルを読み込み画像1画面分表示
	JP	OR_GRAM2_TO_GRAM1		; USR($C82D)    GRAM2の内容をGRAM1に合成します
	LD	DE, 0A7E0h
	JP	SD_USR_OPEN_CONCAT		; WICS用 USR($C830,0)
	LD	DE, 0A7FFh
	JP	SD_USR_NEXT_FILE_EXIST		; WICS用 USR($C836,0)

; GVRAMにあるデータをRAMに転送する
; GVRAM to RAM 06000h〜 8000bytes → 0CF00h〜
; GRAPH Iで指定されているページがRAMに転送されます
GVRAMTORAM:
	LD	(RESTORE_STACK1 + 1), SP
	LD	SP, STACK

	; backup port 0E8h
	IN	A, (0E8h)
	LD	(BACKUP_PORT_0E8), A

	; 06000h〜07FFFhをGVRAMに切り替え
	IN	A, (0E8h)
	OR	0C0h
	OUT	(0E8h), A

	; 06000h〜8000bytesを0CF00h〜に転送
	LD	HL, 06000h
	LD	DE, 0CF00h
	LD	BC, 8000
	LDIR

	; 06000h〜07FFFhをRAMに戻す
	LD	A, (BACKUP_PORT_0E8)
	OUT	(0E8h), A

	; restore SP
RESTORE_STACK1:
	LD	SP, 0000
	RET

; RAMにあるデータをGVRAMに転送する
; RAM to GVRAM 0CF00h〜 8000bytes → 06000h〜
; RAMからGRAPH Iで指定されているページに転送されます
RAMTOGVRAM:
	LD	(RESTORE_STACK2 + 1), SP
	LD	SP, STACK

	; backup port 0E8h
	IN	A, (0E8h)
	LD	(BACKUP_PORT_0E8), A

	; 06000h〜07FFFhをGVRAMに切り替え
	OR	0C0h
	OUT	(0E8h), A

	; 0CF00h〜8000bytesを06000h〜に転送
	LD	HL, 0CF00h
	LD	DE, 06000h
	LD	BC, 8000
	LDIR

	; 06000h〜07FFFhをRAMに戻す
	LD	A, (BACKUP_PORT_0E8)
	OUT	(0E8h), A

	; restore SP
RESTORE_STACK2:
	LD	SP, 0000
	RET

OR_GRAM2_TO_GRAM1:

	; GVRAM2をBUFにコピーする

	; backup port 0E8h
	IN	A, (0E8h)
	LD	(BACKUP_PORT_0E8), A

	; 06000h〜07FFFhをGVRAMに切り替え
	OR	0C0h
	OUT	(0E8h), A

	; ページ2アクセス
	LD	A, 0Fh
	OUT	(0F4h), A

	; 06000h〜8000bytesを0CF00h〜に転送
	LD	HL, 06000h
	LD	DE, 0CF00h
	LD	BC, 8000
	LDIR

	; ページ1アクセス
	LD	A, 0Eh
	OUT	(0F4h), A

	; 0CF00h〜を0600h〜にORする
	LD	HL, 06000h
	LD	DE, 0CF00h
	LD	BC, 8000

LOOP:
	LD	A, (DE)
	OR	(HL)
	LD	(HL), A
	INC	DE
	INC	HL
	DEC	BC
	LD	A, B
	OR	C
	JP	NZ, LOOP

	; 06000h〜07FFFhをRAMに戻す
	LD	A, (BACKUP_PORT_0E8)
	OUT	(0E8h), A

	RET

; デコードする
; HL: lzeデータ + 4
DECORD:
	LD	DE, 0CF00h
	CALL	DECODE_LZE
	RET

BACKUP_PORT_0E8:
	DB	0

INCLUDE "lzdec.mac"
INCLUDE "MZ2000SD.ASM"
INCLUDE "MMZDECORD80B.ASM"

	ORG	0CF00h
STACK:

	END	000B1h
